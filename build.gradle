plugins {
    id 'java'
    id 'org.springframework.boot' version '3.2.2'
    id 'io.spring.dependency-management' version '1.1.4'
    id 'maven-publish'
}

group = 'com.erpmicroservices'
version = '0.0.1-SNAPSHOT'
description = 'people_and_organizations-endpoint-graphql'
java.sourceCompatibility = '21'
ext {
    postgresqlVersion = '42.7.2'
    graphqlJavaExtendedScalarsVersion = "20.0"
    commonsCollections4 = "4.4"
    cucumberVersion = "7.15.0"
}

repositories {
//    mavenLocal()
    mavenCentral()

//    maven {
//        url = uri('https://maven.pkg.github.com/ErpMicroServices')
//    }
}

sourceSets {
    behaviorTest {
        compileClasspath += sourceSets.main.output
        runtimeClasspath += sourceSets.main.output
    }
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-graphql'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation "org.postgresql:postgresql:$postgresqlVersion"
    implementation 'org.projectlombok:lombok'
    implementation "com.graphql-java:graphql-java-extended-scalars:$graphqlJavaExtendedScalarsVersion"
    implementation "org.apache.commons:commons-collections4:$commonsCollections4"
    annotationProcessor 'org.projectlombok:lombok'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.graphql:spring-graphql-test'
    behaviorTestImplementation 'org.springframework.boot:spring-boot-starter-test'
    behaviorTestImplementation 'org.springframework.graphql:spring-graphql-test'
    behaviorTestImplementation "io.cucumber:cucumber-java:$cucumberVersion"
    behaviorTestImplementation "io.cucumber:cucumber-junit:$cucumberVersion"
    behaviorTestImplementation 'org.junit.vintage:junit-vintage-engine'
}

configurations {
    behaviorTestTestImplementation.extendsFrom testImplementation
    behaviorTestTestRuntimeOnly.extendsFrom testRuntimeOnly
}

publishing {
    publications {
        maven(MavenPublication) {
            from(components.java)
        }
    }
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

tasks.withType(Javadoc) {
    options.encoding = 'UTF-8'
}

tasks.register('behaviorTest', Test) {
    description = 'Runs integration tests.'
    group = 'verification'

    shouldRunAfter test

    useJUnitPlatform()

    testLogging {
        events "passed"
    }
}

check.dependsOn behaviorTest
