plugins {
    id 'java'
    id 'org.springframework.boot' version '3.2.2'
    id 'io.spring.dependency-management' version '1.1.4'
    id 'com.netflix.dgs.codegen' version '6.0.3'
}

group = 'com.erpmicroservices'
version = '0.0.1-SNAPSHOT'
description = 'people_and_organizations-endpoint-graphql'
java.sourceCompatibility = '21'

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
    behaviorTestTestImplementation.extendsFrom implementation, testImplementation
    behaviorTestTestRuntimeOnly.extendsFrom runtimeOnly, testRuntimeOnly
}

ext {
    set('springCloudVersion', "2023.0.0")
    set('queryDslVersion', '6.0')
}


repositories {
    mavenCentral()
}

sourceSets {
    behaviorTest {
        compileClasspath += sourceSets.main.output + sourceSets.test.output
        runtimeClasspath += sourceSets.main.output + sourceSets.test.output
    }
}

test {
    useJUnitPlatform()
}

dependencies {

    implementation 'org.springframework:spring-beans'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-graphql'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'com.graphql-java:graphql-java-extended-scalars:19.0'

    compileOnly 'org.projectlombok:lombok'
    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    runtimeOnly 'org.postgresql:postgresql'
    annotationProcessor 'org.projectlombok:lombok'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.boot:spring-boot-test'
    testImplementation 'org.springframework.boot:spring-boot-testcontainers'
    testImplementation 'org.springframework:spring-webflux'
    testImplementation 'org.springframework.graphql:spring-graphql-test'
    testImplementation 'org.testcontainers:junit-jupiter'
    testImplementation 'org.testcontainers:postgresql'

    behaviorTestImplementation 'io.cucumber:cucumber-java:7.11.1'
    behaviorTestImplementation 'io.cucumber:cucumber-junit:7.11.1'
    behaviorTestImplementation 'io.cucumber:cucumber-spring:7.11.1'
    behaviorTestImplementation "org.testcontainers:testcontainers:1.18.3"
    behaviorTestImplementation 'org.testcontainers:postgresql:1.18.3'
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
}

tasks.register('behaviorTest', Test) {

    description = 'Runs Behavior tests.'
    group = 'verification'

    testClassesDirs = sourceSets.behaviorTest.output.classesDirs
    classpath = sourceSets.behaviorTest.runtimeClasspath
    dependsOn assemble
    shouldRunAfter test

    useJUnitPlatform()

    testLogging {
        events "passed"
    }
}

check.dependsOn behaviorTest

generateJava {
    schemaPaths = ["${projectDir}/src/main/resources/graphql-client"]
    packageName = 'org.erpmicroservices.peopleandorganizations.endpoint.graphql'
    generateClient = true
}

